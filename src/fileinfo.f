C     FILEINFO.F
C
C     THIS PROGRAM READS THE VOYAGER DATA INPUT FILE AND OUTPUTS VARIOUS
C     STATISTICS ABOUT THE FILE.
C
C     OUTPUT FORMAT:
C       LINES:     NUMBER OF LINES IN THE INPUT FILE
C       FILL:      NUMBER OF LINES CONTAINING NO DATA
C       DATA:      NUMBER OF LINES CONTAINING DATA
C       MISSING:   NUMBER OF HOURS WHERE NO DATA IS AVAILABLE
C       MAX GAP:   LARGEST CONTIGUOUS STRETCH OF MISSING DATA
C       DATA SIZE: SIZE OF THE DATASET, INCLUDING MISSING DATA
C
C     COPYRIGHT (C) 2018 JEFF SPAULDING <SARNET@GMAIL.COM>
C
C     THIS IS THE ISC LICENSE.
C
C     PERMISSION TO USE, COPY, MODIFY, AND DISTRIBUTE THIS SOFTWARE FOR
C     ANY PURPOSE WITH OR WITHOUT FEE IS HEREBY GRANTED, PROVIDED THAT
C     THE ABOVE COPYRIGHT NOTICE AND THIS PERMISSION NOTICE APPEAR IN
C     ALL COPIES.
C
C     THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL
C     WARRANTIES WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED
C     WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE
C     AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT, INDIRECT, OR
C     CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
C     LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT,
C     NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN
C     CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

      PROGRAM FILEINFO

      IMPLICIT NONE

      INTEGER LINES,GAP,BLANKS,MAXGAP,SKIP

      INTEGER SCID,YEAR,DAY,HOUR
      REAL XCOORD,YCOORD,ZCOORD,DIST,INTENS,MODULU,LAMBDA,DELTA

      INTEGER TS,OLDTS,TSBEG,TSTAMP

      CHARACTER FILENAME*18
      PARAMETER(FILENAME='data/VY1MAG_1H.ASC')

      LINES=0
      BLANKS=0
      GAP=0
      SKIP=0
      MAXGAP=0
      TS=0
      OLDTS=0
      TSBEG=0

      OPEN(10,FILE=FILENAME,ERR=800,STATUS='OLD')

C     FILE READ LOOP BEGIN
 10   CONTINUE

      READ(10,*,ERR=801,END=20)SCID,YEAR,DAY,HOUR,XCOORD,YCOORD,ZCOORD,
     C     DIST,INTENS,MODULU,LAMBDA,DELTA

      LINES=LINES+1

      IF (DIST.LT.0.1)THEN
         BLANKS=BLANKS+1
      ELSE
         OLDTS=TS
         TS=TSTAMP(YEAR,DAY,HOUR)

         IF(OLDTS.GT.0)THEN
            GAP=TS-OLDTS
            SKIP=SKIP+GAP-1

            IF(GAP.GT.MAXGAP)THEN
               MAXGAP=TS-OLDTS
            END IF
         ELSE
            TSBEG=TS
         END IF
      END IF

      GOTO 10
C     FILE READ LOOP END

 20   CLOSE(10)

      WRITE(*,FMT='(A7,A7,A7,A9,A9,A11)')'LINES','FILL',
     C     'DATA','MISSING','MAX GAP','DATA SIZE'
      WRITE(*,FMT='(I7,I7,I7,I9,I9,I11)')LINES,BLANKS,LINES-BLANKS,
     C     SKIP,MAXGAP,TS-TSBEG+1

C     NORMAL EXIT OF PROGRAM
      STOP

C     JUMP POINT FOR OPEN FAILURE
 800  WRITE(*,*)'CANNOT OPEN FILE, ABORTING.'
      STOP

C     JUMP POINT FOR READ FAILURE
 801  WRITE(*,*)'ERROR READING FILE, ABORTING.'
      CLOSE(10)
      STOP

      END


      INTEGER FUNCTION TSTAMP(Y,D,H)

C     GENERATE A TIMESTAMP BASED ON YEAR, DAY, AND HOUR
C     TIMESTAMPS ARE HOURLY FROM MIDNIGHT, JAN 1, 1977

      INTEGER Y,D,H

      TSTAMP=0

 110  IF(Y.GT.77)THEN
         Y=Y-1
         IF(MOD(Y,4).EQ.0)THEN
            TSTAMP=TSTAMP+366*24
         ELSE
            TSTAMP=TSTAMP+365*24
         END IF
         GOTO110
      END IF

      TSTAMP=TSTAMP+D*24+H

      END
